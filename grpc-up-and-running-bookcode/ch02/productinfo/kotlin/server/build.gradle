buildscript {
    ext.kotlin_version = '1.5.21'
    ext.coroutines_version = '1.5.0'
    ext.protobuf_version = '3.17.2'
    ext.grpc_version = '1.39.0' // CURRENT_GRPC_VERSION
    ext.grpc_kotlin_version = '1.1.0' // CURRENT_GRPC_KOTLIN_VERSION

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.17'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'
apply plugin: 'application'

repositories {
    google()
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation "io.grpc:grpc-kotlin-stub:${grpc_kotlin_version}"

//    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.61"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutines_version}"

    // gRPC and Protobuf
    implementation 'com.google.protobuf:protobuf-gradle-plugin:0.8.17'
    implementation "com.google.protobuf:protobuf-java:${protobuf_version}"
    implementation "com.google.protobuf:protobuf-java-util:${protobuf_version}"
    implementation "io.grpc:grpc-netty-shaded:${grpc_version}"
    implementation "io.grpc:grpc-protobuf:${grpc_version}"
    implementation "io.grpc:grpc-stub:${grpc_version}"

    compileOnly "javax.annotation:javax.annotation-api:1.2"

    // Google
    implementation "com.google.guava:guava:28.2-jre"
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protobuf_version}" }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpc_version}"
        }
        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:${grpc_kotlin_version}:jdk7@jar"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                // Generate Java gRPC classes
                grpc { }
                // Generate Kotlin gRPC using the custom plugin from library
                grpckt { }
            }
        }
    }
}

startScripts.enabled = false

task productInfoServer(type: CreateStartScripts) {
    mainClassName = 'ecommerce.ProductInfoServerKt'
    applicationName = 'productinfo-server'
    outputDir = startScripts.outputDir
    classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
    from(productInfoServer)
    fileMode = 0755
}